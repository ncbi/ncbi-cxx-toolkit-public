#############################################################################
#  $RCSfile$  $Revision$  $Date$
# ==========================================================================
#
#                            PUBLIC DOMAIN NOTICE
#               National Center for Biotechnology Information
#
#  This software/database is a "United States Government Work" under the
#  terms of the United States Copyright Act.  It was written as part of
#  the author's official duties as a United States Government employee and
#  thus cannot be copyrighted.  This software/database is freely available
#  to the public for use. The National Library of Medicine and the U.S.
#  Government have not placed any restriction on its use or reproduction.
#
#  Although all reasonable efforts have been taken to ensure the accuracy
#  and reliability of the software and data, the NLM and the U.S.
#  Government do not and cannot warrant the performance or results that
#  may be obtained by using this software or data. The NLM and the U.S.
#  Government disclaim all warranties, express or implied, including
#  warranties of performance, merchantability or fitness for any particular
#  purpose.
#
#  Please cite the author in any work or product based on this material.
#
# ==========================================================================
#
# Author:  Denis Vakatov
#
# File Description:
#   Generate platform-dependent header("config.h") for NCBI C++ libs
#   USAGE:
#    1) Process this file with "autoconf" to produce a "configure" script.
#    2) Run the resultant "configure" script to produce
#      a) ".../inc/ncbiconf.h"
#      b) ".../build/Makefile.mk"
#      c) other "Makefile"s and scripts in ".../build"
#
#############################################################################

AC_INIT(include/corelib/ncbitype.h)


#############################################################################


#### Get the running host's properties
AC_CANONICAL_HOST
AC_DEFINE_UNQUOTED(HOST,        "$host")
AC_DEFINE_UNQUOTED(HOST_CPU,    "$host_cpu")
AC_DEFINE_UNQUOTED(HOST_VENDOR, "$host_vendor")
AC_DEFINE_UNQUOTED(HOST_OS,     "$host_os")


#### C and C++ compilers
AC_LANG_CPLUSPLUS
AC_PROG_CC
AC_PROG_CXX


#### Detect supported compilers
changequote(, )dnl
compiler=
compiler_ver=
if test "$GCC" = "yes" ; then
   compiler_ver="`$CXX --version 2>&1`"
   case "$compiler_ver" in
     2\.95* )  compiler="GCC" ;;
   esac
else
   case "$host" in
    sparc-sun-solaris2\.[67]  |  i*86-pc-solaris2\.[67] )
      compiler_ver="`$CXX -V 2>&1`"
      case "$compiler_ver" in
        *WorkShop*Compilers*5\.*)  compiler="WorkShop5" ;;
      esac ;;

    mips-sgi-irix6\.* )
      compiler_ver="`$CXX -version 2>&1`"
      case "$compiler_ver" in
        MIPSpro*Compilers:*Version*7\.3* )  compiler="MIPSpro73" ;;
      esac ;;

    i*86*-pc-cygwin32 )
      case "$CXX" in
        *cl\.exe* | cl)  compiler="MSVC" ;; 
      esac ;;
   esac
fi
changequote([, ])dnl


#### Additional check for pre-defined compilers
case "$compiler" in
  WorkShop5 | MIPSpro73 | MSVC )
    if test -z "$NCBI_COMPILER" ; then
       echo
       echo "Compiler \"${compiler}\" requires a special tuning, so you"
       echo "better use a special shell script located in \"compilers/*.sh\"!"
       echo
       echo "Do you still want to proceed? [n/y]"
       read answer
       case "$answer" in
         y )  AC_MSG_WARN([Proceeding at your own risk...]) ;;
         * )  AC_MSG_WARN([Configuration has been canceled by user.]) ; exit ;;
       esac
       echo
    fi ;;
esac



#### Use full path to the compiler, if possible
set $CC
x_CC="`which $1`"
if test -x "$x_CC" ; then
  shift
  CC="$x_CC $*"
  echo "adjusted C   compiler: $CC"
fi
x_CC=

set $CXX
x_CXX="`which $1`"
if test -x "$x_CXX" ; then
  shift
  CXX="$x_CXX $*"
  echo "adjusted C++ compiler: $CXX"
fi
x_CXX=


#### Provide default environment setup for known platforms/compilers
compiler_extra=
case "$host:$compiler" in
 sparc-sun-solaris2\.6:GCC )
   echo "GCC compiler is not supported for:  $host"
   compiler=
   ;;

 *-linux*:GCC )
   NETWORK_LIBS=${NETWORK_LIBS:=" "}
   ;;

 *-osf*:GCC )
   CXXFLAGS="-fsquangle $CXXFLAGS"
   NETWORK_LIBS=${NETWORK_LIBS:=" "}
   ;;

 *:WorkShop5 )
   with_bincopy=${with_bincopy:="no"}
   if test -n "$WS64_CFLAGS" ; then
      if test "$host" != "sparc-sun-solaris2.7" ; then
        echo "64bit flags are for sparc-sun-solaris2.7 only:  $WS64_CFLAGS"
        exit 1
      fi
      compiler_extra="_64"
      with_internal="no"
   else
      compiler_extra="_32"
   fi
   CFLAGS="-mt $WS64_CFLAGS"
   CXXFLAGS="-mt +w +w2 $WS64_CFLAGS"
   LDFLAGS="-xildoff $WS64_CFLAGS $LDFLAGS"
   AR="$CXX -xar -o"
   STRIP=":"
   if test "$with_debug" = "no" ; then
      LDFLAGS="-s $LDFLAGS"
   fi
   RANLIB=":"
   CXX_FILTER="2>&1 | $(top_srcdir)/compilers/cxx_filter.WorkShop5.sh"
   serial_ws50_rtti_kludge="rtti"
   ;;

 *:MIPSpro73 )
   COMMON_FLAGS="-fullwarn -signed -brief_diagnostics -use_readonly_const -G0 -rdata_shared -n32"
   CFLAGS="$COMMON_FLAGS -ansi"
   CXXFLAGS="$COMMON_FLAGS -ansiW -LANG:std -LANG:ansi-for-init-scope -woff 1460,1521,1429,1169,1209,1107,1424"
   CPPFLAGS="-D__LONGLONG $CPPFLAGS"
   LDFLAGS="-LANG:std $LDFLAGS"
   AR=${AR:="$CXX -ar -WR,-v -o"}
   ;;

 i*86*-pc-cygwin32:GCC )
   with_dll="no"
   NETWORK_LIBS=${NETWORK_LIBS:=" "}
   CONF_exe_ext=".exe"
   ;;

 i*86*-pc-cygwin32:MSVC )
   with_dll="no"
   CFLAGS="/W4 /Od /Zi /DEBUG /D_DEBUG"
   CXXFLAGS="/W4 /Od /Zi /DEBUG /D_DEBUG"
   CPPFLAGS="/nologo $CPPFLAGS"
   LDFLAGS=${LDFLAGS:="/DEBUG"}
   AR="lib.exe"
   RANLIB=":"
   STRIP=":"
   CONF_obj_ext=".obj"
   CONF_lib_ext=".lib"
   CONF_lib_l_ext=".lib"
   CONF_lib_pre=" "
   CONF_lib_l_pre=" "
   CONF_exe_ext=".exe"
   CONF_f_compile="/c "
   CONF_f_outobj="/Fo"
   CONF_f_outlib="/OUT:"
   CONF_f_libpath="/LIBPATH:"
   CONF_f_outexe="/OUT:"
   script_shell=""
   make_shell=""
   ;;
esac


#### Check if the compiler matches one of supported compilers on this platform
if test -z "$compiler" ; then
  AC_MSG_ERROR(["Compiler $CXX $compiler_ver is not supported. Consult doc/config.html#ref_Compilers for the list of supported platforms/compilers"])
fi


#### DLLs
if test "$with_dll" != "no" ; then
   case "$compiler" in
    GCC )
      CFLAGS="$CFLAGS -fPIC"
      CXXFLAGS="$CXXFLAGS -fPIC"
      ;;
    WorkShop5 | MIPSpro73 )
      CFLAGS="$CFLAGS -KPIC"
      CXXFLAGS="$CXXFLAGS -KPIC"
      ;;
   esac
fi

if test "$with_dll" = "yes" ; then
   lib_dll="dll"
   CONF_lib_ext="${CONF_dll_ext:=.so}"

   case "$compiler" in
    GCC | MIPSpro73 )
      LINKDLL="$CXX -shared -o"
      ;;
    WorkShop5 )
      LINKDLL="$CXX $WS64_CFLAGS -G -h \$@ -o"
      ;;
    * )
      if test -z "$LINKDLL" ; then
         AC_MSG_ERROR(["Dont know how to build DLLs with compiler $CXX $compiler_ver"])
      fi
      ;;
   esac
fi

if test "$with_dll" = "yes" ; then
   case "$host:$compiler" in
    *-linux*:GCC | mips-sgi-irix6\.*:GCC | *:WorkShop5 | *:MIPSpro73 )
      ;;
    * )
         AC_MSG_WARN(["DLLs may not be buildable by $CXX $compiler_ver on $host"])
      ;;
   esac
fi


#### GNU compiler::  common flags and definitions
if test "$GCC" = "yes" ; then
   CFLAGS="-Wall $CFLAGS"
   CXXFLAGS="-Wall $CXXFLAGS"
   AC_DEFINE_UNQUOTED(__EXTENSIONS__, 1)
   AC_DEFINE_UNQUOTED(_SVID_SOURCE,   1)
fi


#### SUN "make" utility only
case "$host_os" in
  solaris* )  KeepStateTarget=".KEEP_STATE:" ;;
esac


#### Tools
AC_PROG_RANLIB
AC_CHECK_PROG(AR, ar, ar cru, ErrorCannotFind_AR)
AC_CHECK_PROG(STRIP, strip, strip, :)


#### Check if "${exec_prefix}" is defined;  provide a default one
if test -z "${exec_prefix}"  ||  test "${exec_prefix}" = "NONE" ; then
   exec_prefix="${srcdir}/${compiler}${compiler_extra}"
else
   with_debug_sfx=${with_debug_sfx:="no"}
fi


#### Add "Release"/"Debug" suffix to "${exec_prefix}"
if test "$with_debug_sfx" != "no" ; then
   if test "$with_debug" = "no" ; then
      exec_prefix="${exec_prefix}-Release"
   else
      exec_prefix="${exec_prefix}-Debug"
   fi
fi


#### Add full host specs to "${exec_prefix}"
if test "$with_hostspec" = "yes" ; then
   exec_prefix="${exec_prefix}--${host}"
fi


#### Check if there is "${exec_prefix}" dir
test -d "${exec_prefix}"  ||  mkdir -p "${exec_prefix}"


#### Make "exec_prefix" absolute
case "${exec_prefix}" in
   /* ) ;;
   . )
      CDPATH=
      if test "${PWD}" != "" && test "`(cd ${PWD} ; sh -c pwd)`" = "`pwd`"  ;
      then
         exec_prefix="$PWD"
      else
         exec_prefix="`(cd ${exec_prefix}; pwd)`"
      fi
   ;;
   *  ) exec_prefix="`(cd ${exec_prefix}; pwd)`" ;;
esac


#### Make "srcdir" absolute
case "${srcdir}" in
   /* ) ;;
   . )
      CDPATH=
      if test "${PWD}" != "" && test "`(cd ${PWD} ; sh -c pwd)`" = "`pwd`"  ;
      then
        srcdir="$PWD"
      else
        srcdir="`(cd ${srcdir}; pwd)`"
      fi
   ;;
   * ) srcdir="`(cd ${srcdir}; pwd)`" ;;
esac


#### Source, include, and build directories
ncbiconf="$exec_prefix/inc/ncbiconf.h"
if test "$exec_prefix" = "$srcdir" ; then
   builddir="$srcdir/src"
else
   builddir="$exec_prefix/build"
fi


#### Create build directories
mkdir -p "$builddir" "$exec_prefix/lib" "$exec_prefix/bin"


#### Check if the target directory already has a configured system in it
if test -f "${ncbiconf}" ; then
   echo
   echo "There is already a built \"${ncbiconf}\"!"
   echo "Do you want to re-configure in \"${builddir}\"? [y/n]"
   read answer
   case "$answer" in
      y | Y | yes | YES ) ;;
      * )  AC_MSG_WARN([Re-configuration has been canceled by user.]) ; exit ;;
   esac
   echo
fi


#### Use right command shell in scripts and makefiles
script_shell="#! ${CONFIG_SHELL:=/bin/sh}"
make_shell="SHELL=${CONFIG_SHELL:=/bin/sh}"


#### Turn on compilation warnings and adjust
#### some compiler-specific flags and features

if test "$CONF_obj_ext"   = " " ;  then obj_ext="" ; else obj_ext="${CONF_obj_ext:=.o}" ; fi
if test "$CONF_lib_pre"   = " " ;  then lib_pre="" ; else lib_pre="${CONF_lib_pre:=lib}" ; fi
if test "$CONF_lib_l_pre" = " " ;  then lib_l_pre="" ; else lib_l_pre="${CONF_lib_l_pre:=-l}" ; fi
if test "$CONF_lib_ext"   = " " ;  then lib_ext="" ; else lib_ext="${CONF_lib_ext:=.a}" ; fi
if test "$CONF_lib_l_ext" = " " ;  then lib_l_ext="" ; else lib_l_ext="${CONF_lib_l_ext}" ; fi
if test "$CONF_exe_ext"   = " " ;  then exe_ext="" ; else exe_ext="${CONF_exe_ext}" ; fi
if test "$CONF_f_compile" = " " ;  then f_compile="" ; else f_compile="${CONF_f_compile:=-c }" ; fi
if test "$CONF_f_outobj"  = " " ;  then f_outobj="" ; else f_outobj="${CONF_f_outobj:=-o }" ; fi
if test "$CONF_f_outlib"  = " " ;  then f_outlib="" ; else f_outlib="${CONF_f_outlib}" ; fi
if test "$CONF_f_libpath" = " " ;  then f_libpath="" ; else f_libpath="${CONF_f_libpath:=-L}" ; fi
if test "$CONF_f_outexe"  = " " ;  then f_outexe="" ; else f_outexe="${CONF_f_outexe:=-o }" ; fi
lib_dll="${lib_dll:=lib}"


#### For some compilers(e.g. SunPro 5.0), there is no sense to compare the
#### old object/lib/binary file with the new one -- they will always be
#### different because of some timestamp or smth. else ("--without-bincopy")
BINCOPY="${CONFIG_SHELL-/bin/sh} \$(top_srcdir)/scripts/if_diff.sh \"cp -p\""
if test "$with_bincopy" = "no" ; then
   BINCOPY="cp -p"
fi


#### Manage the debug-vs.-optimization flags ("--*-debug", "--*-optimization")
changequote(, )dnl
if test "$compiler" != "MSVC" ; then
   if test "$with_debug" = "no" ; then
      with_optimization=${with_optimization:="yes"}
      CFLAGS=`echo $CFLAGS     | sed 's/-g[0-9]*//g'`
      CXXFLAGS=`echo $CXXFLAGS | sed 's/-g[0-9]*//g'`
      LDFLAGS=`echo $LDFLAGS   | sed 's/-g[0-9]*//g'`
   else
      with_optimization=${with_optimization:="no"}
      if echo "$CFLAGS"   |grep -v >/dev/null '\-g' ; then
         CFLAGS="$CFLAGS -g"     ; fi
      if echo "$CXXFLAGS" |grep -v >/dev/null '\-g' ; then
         CXXFLAGS="$CXXFLAGS -g" ; fi
      if test "$with_optimization" = "no" ; then
         CPPFLAGS="-D_DEBUG $CPPFLAGS"
      fi
      if echo "$LDFLAGS"  |grep -v >/dev/null '\-g' ;
         then LDFLAGS="$LDFLAGS -g"   ; fi
      STRIP=":"
   fi

   if test "$with_optimization" = "no" ; then
      CFLAGS=`echo $CFLAGS     | sed 's/-O[0-9]*//g'`
      CXXFLAGS=`echo $CXXFLAGS | sed 's/-O[0-9]*//g'`
      LDFLAGS=`echo $LDFLAGS   | sed 's/-O[0-9]*//g'`
   else
      if echo "$CXXFLAGS" |grep -v >/dev/null '\-O' ; then
         CXXFLAGS="$CXXFLAGS -O" ; fi
      if echo "$CFLAGS"   |grep -v >/dev/null '\-O' ; then
         CFLAGS="$CFLAGS -O" ; fi
      CPPFLAGS="-DNDEBUG $CPPFLAGS"
      if echo "$LDFLAGS"  |grep -v >/dev/null '\-O' ; then
         LDFLAGS="$LDFLAGS -O"   ; fi
   fi

   if test "$with_debug" = "no"  &&  test "$with_optimization" = "no" ; then
      CPPFLAGS="-DNDEBUG $CPPFLAGS"
   fi
fi
changequote([, ])dnl



### ------------------------------------------------------------------
### ------------------------------------------------------------------

dnl Check for header files.
dnl ---------------------------------
AC_CHECK_HEADERS(iostream  iostream.h)
AC_CHECK_HEADERS(fstream   fstream.h)
AC_CHECK_HEADERS(strstream strstream.h strstrea.h)


dnl Check for the platform (very roughly)
dnl ---------------------------------
AC_CHECK_HEADERS(unistd.h,  x_have_unistd="yes")
AC_CHECK_HEADERS(windows.h, x_have_windows="yes")

if test "$x_have_windows" = "yes" ; then
   AC_DEFINE_UNQUOTED(NCBI_OS_MSWIN, 1)
   AC_DEFINE_UNQUOTED(NCBI_OS, "MSWIN")
elif test "$x_have_unistd" = "yes" ; then
   AC_DEFINE_UNQUOTED(NCBI_OS_UNIX, 1)
   AC_DEFINE_UNQUOTED(NCBI_OS, "UNIX")
else
   AC_DEFINE_UNQUOTED(NCBI_OS_MAC, 1)
   AC_DEFINE_UNQUOTED(NCBI_OS, "MAC")
fi


dnl Check for some library functions.
dnl ------------------------------------------------------
AC_MSG_CHECKING(for gethostbyname_r)
AC_TRY_COMPILE(
   [#include <netdb.h>],
   [ struct hostent x_hp;
     char   x_buf[1024];
     int    x_err;
     struct hostent *hp =
         gethostbyname_r("localhost", &x_hp, x_buf, sizeof(x_buf), &x_err); ],
   AC_DEFINE_UNQUOTED(HAVE_GETHOSTBYNAME_R, 1)
)
AC_MSG_RESULT()


AC_MSG_CHECKING(for strdup)
AC_TRY_COMPILE(
   [#include <string.h>],
   [ const char s[] = "abc";
     char* s1 = strdup(s); ],
   AC_DEFINE_UNQUOTED(HAVE_STRDUP, 1)
)
AC_MSG_RESULT()


dnl Check for typedefs, structures, and compiler features.
dnl ------------------------------------------------------
AC_CXX_CONST
AC_TYPE_SIZE_T

AC_MSG_CHECKING(if in-class template functions are supported)
AC_TRY_COMPILE(
   [class C { template<class T> void operator<<(const T& t) {} };],
   ,
   ,
   AC_DEFINE_UNQUOTED(NO_INCLASS_TMPL, 1)
)
AC_MSG_RESULT()


AC_MSG_CHECKING(if can use exception specifications throw(...) in func.proto)
AC_TRY_RUN(
   [template <class T> inline int test_throw_spec(const T&) throw() {
      return 0;
   }
   int main(void) {
      return test_throw_spec(123);
   }],
   AC_DEFINE_UNQUOTED(NCBI_USE_THROW_SPEC, 1)
)
AC_MSG_RESULT()


AC_MSG_CHECKING(if have an obsolete basic_string::compare())
AC_TRY_COMPILE(
   [#include <string>],
   [std::string s;  int i = 0;  i = s.compare("aaa", 0, 2); ],
   AC_DEFINE_UNQUOTED(NCBI_OBSOLETE_STR_COMPARE, 1)
)
AC_MSG_RESULT()


AC_MSG_CHECKING(if the auto_ptr template class is poor or not implemented)
AC_TRY_COMPILE(
   [#include <memory>],
   [int *ip = 0;  std::auto_ptr<int> ap(ip);  *ip = *ap;  ap.reset(ip); ],
   ,
   AC_DEFINE_UNQUOTED(HAVE_NO_AUTO_PTR, 1)
)
AC_MSG_RESULT()


AC_MSG_CHECKING(if new C++ streams dont have ios_base::)
AC_TRY_COMPILE(
   [#include <iostream>],
   [(void)(std::ios_base::out != std::ios_base::beg); ],
   ,
   AC_DEFINE_UNQUOTED(HAVE_NO_IOS_BASE, 1)
)
AC_MSG_RESULT()


AC_MSG_CHECKING(for std::char_traits::)
AC_TRY_COMPILE(
   [#include <iostream>],
   [using namespace std;
   char_traits<char>::int_type  i = char_traits<char>::eof();
   i = char_traits<char>::not_eof(i);
   char_traits<char>::char_type c = '\0';
   char_traits<char>::pos_type  p = 0;
   char_traits<char>::off_type  o = 1;
   ],
   ,
   AC_DEFINE_UNQUOTED(HAVE_NO_CHAR_TRAITS, 1)
)
AC_MSG_RESULT()


dnl Check for the availability of other packages
dnl --------------------------------------------

### System and miscellaneous libs
THREAD_LIBS=${THREAD_LIBS:="-lpthread"}
NETWORK_LIBS=${NETWORK_LIBS:="-lsocket -lnsl"}


### Save original compilator/linker flags
###
orig_CPPFLAGS="$CPPFLAGS"
orig_LIBS="$LIBS"


## SYBASE libraries
AC_MSG_CHECKING(if SYBASE libraries are available)
if test "$with_sybase" != "no" ; then
   SYBASE_PATH=${SYBASE_PATH:="/netopt/Sybase/clients/current"}
   SYBASE_INCLUDE="-I$SYBASE_PATH/include"
   SYBASE_LIBS="-L$SYBASE_PATH/lib -lct -lcs -ltcl -lcomn -lintl"
   CPPFLAGS="$SYBASE_INCLUDE $orig_CPPFLAGS"
   LIBS="$SYBASE_LIBS $NETWORK_LIBS $orig_LIBS"
   AC_TRY_LINK(
      [#include <ctpublic.h>],
      [CS_RETCODE i = ct_init(0,0);  i = cs_ctx_global(0,0); ],
      [AC_DEFINE_UNQUOTED(HAVE_LIBSYBASE, 1)],
      [with_sybase="no"]
   )
fi
if test "$with_sybase" = "no" ; then
   SYBASE_PATH="No_Sybase"
   SYBASE_INCLUDE=
   SYBASE_LIBS=
fi
AC_MSG_RESULT([$SYBASE_PATH])


## NCBI C Toolkit
AC_MSG_CHECKING(if NCBI C toolkit is available)
if test "$with_ncbi_c" != "no" ; then
   NCBI_C_PATH=${NCBI_C_PATH:="$NCBI"}
   NCBI_C_INCLUDE="-I$NCBI_C_PATH/include"
   if test "$with_debug" = "no" ; then
     NCBI_C_LIBPATH="-L$NCBI_C_PATH/lib"
   else
     NCBI_C_LIBPATH="-L$NCBI_C_PATH/altlib"
   fi
   CPPFLAGS="$NCBI_C_INCLUDE $orig_CPPFLAGS"
   LIBS="$NCBI_C_LIBPATH -lncbi $NETWORK_LIBS $orig_LIBS"
   AC_TRY_LINK(
      [#include <ncbi.h>],
      [ Nlm_Boolean b = Nlm_GetArgs("fake", -1, 0); ],
      [AC_DEFINE_UNQUOTED(HAVE_NCBI_C, 1)],
      [with_ncbi_c="no"]
   )
fi
if test "$with_ncbi_c" = "no" ; then
   NCBI_C_PATH="No_NCBI_CToolkit"
   NCBI_C_INCLUDE=
   NCBI_C_LIBPATH=
   NCBI_C_ncbi=
else
   NCBI_C_ncbi=${NCBI_C_ncbi:="-lncbi"}
fi
AC_MSG_RESULT([$NCBI_C_PATH])


## Inhouse Fast-CGI library
AC_MSG_CHECKING(if Fast-CGI library is available)
if test "$with_fastcgi" != "no" ; then
   FASTCGI_PATH=${FASTCGI_PATH:="$NCBI"}
   FASTCGI_INCLUDE="-I$FASTCGI_PATH/include"
   if test "$with_debug" = "no" ; then
      FASTCGI_LIBS="-L$FASTCGI_PATH/lib -lfcgi $NETWORK_LIBS"
   else
      FASTCGI_LIBS="-L$FASTCGI_PATH/altlib -lfcgi $NETWORK_LIBS"
   fi
   FASTCGI_OBJS="fcgibuf"
   CPPFLAGS="$FASTCGI_INCLUDE $orig_CPPFLAGS"
   LIBS="$FASTCGI_LIBS $orig_LIBS"
   AC_TRY_LINK(
      [#include <fcgiapp.h>],
      [ (void) FCGX_IsCGI(); ],
      [AC_DEFINE_UNQUOTED(HAVE_LIBFASTCGI, 1)],
      [with_fastcgi="no"]
   )
fi
if test "$with_fastcgi" = "no" ; then
   FASTCGI_PATH="No_FastCgi"
   FASTCGI_INCLUDE=
   FASTCGI_LIBS=
   FASTCGI_OBJS=
fi
AC_MSG_RESULT([$FASTCGI_PATH])


## NCBI SSS DB library
AC_MSG_CHECKING(if NCBI SSS DB library is available)
if test "$with_sybase" = "no" ; then
   with_sssdb="no"
elif test "$with_sssdb" != "no" ; then
   NCBI_SSSDB_PATH=${NCBI_SSSDB_PATH:="/net/neptune/pubmed1/sss/db"}
   NCBI_SSSDB_INCLUDE="-I$NCBI_SSSDB_PATH"
   if test "$with_debug" = "no" ; then
     NCBI_SSSDB_LIBS="-L$NCBI_SSSDB_PATH/Release -lsssdb"
   else
     NCBI_SSSDB_LIBS="-L$NCBI_SSSDB_PATH/Debug -lsssdb"
   fi
   CPPFLAGS="$NCBI_SSSDB_INCLUDE $SYBASE_INCLUDE $orig_CPPFLAGS"
   LIBS="$NCBI_SSSDB_LIBS $SYBASE_LIBS $NETWORK_LIBS $orig_LIBS"
   AC_TRY_LINK(
      [#include <cslink.hpp>],
      [ (new CSLink)->connect2server("srv", "user", "pwd", "db");  return 0; ],
      [AC_DEFINE_UNQUOTED(HAVE_LIBSSSDB, 1)],
      [with_sssdb="no"]
   )
fi
if test "$with_sssdb" = "no" ; then
   NCBI_SSSDB_PATH="No_NCBI_SSSDB"
   NCBI_SSSDB_INCLUDE=
   NCBI_SSSDB_LIBS=
fi
AC_MSG_RESULT([$NCBI_SSSDB_PATH])


# NCBI PubMed
AC_MSG_CHECKING(if NCBI PubMed libraries are available)
if test "$with_ncbi_c" = "no" ; then
   with_pubmed="no"
elif test "$with_pubmed" != "no" ; then
   NCBI_PM_PATH=${NCBI_PM_PATH:="/net/neptune/pubmed1/Cvs5"}
   CPPFLAGS="-I$NCBI_PM_PATH/pmentrez/include/Ncbi $NCBI_C_INCLUDE $orig_CPPFLAGS"
   LIBS="-L$NCBI_PM_PATH/pmentrez/libs -lpmncbi $NCBI_C_LIBPATH -lncbi $orig_LIBS"
   AC_TRY_LINK(
      [#include <ncbistr.hpp>],
      [ CNcbiString("abc"); ],
      [AC_DEFINE_UNQUOTED(HAVE_PUBMED, 1)],
      [with_pubmed="no"]
   )
fi
if test "$with_pubmed" = "no" ; then
   NCBI_PM_PATH="No_NCBI_PubMed"
fi
AC_MSG_RESULT([$NCBI_PM_PATH])


### Restore original compilator/linker flags
###
LIBS="$orig_LIBS"
CPPFLAGS="$orig_CPPFLAGS"


dnl Optional projects
dnl --------------------------------------------

if test "$with_sybase" = "no" || test "$with_ncbi_c" = "no" || test "$with_sssdb" = "no" || test "$with_pubmed" = "no" ; then
  with_internal="no"
fi

if test "$with_internal" = "yes" ; then
   internal="internal"
else
   internal=
fi

if test "$with_serial" = "no" ; then
   serial=
else
   serial="serial"
fi


dnl Check for C standard types an sizes
dnl ---------------------------------
AC_C_BIGENDIAN
AC_C_CHAR_UNSIGNED
AC_CHECK_SIZEOF(void*)
AC_CHECK_SIZEOF(char)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)
AC_CHECK_SIZEOF(__int64)
AC_CHECK_SIZEOF(double)
AC_CHECK_SIZEOF(long double)


dnl Cleanup
dnl -------
if test "$compiler" = "WorkShop5" ; then
  test -d SunWS_cache  &&  rm -r SunWS_cache
fi


#############################################################################
### Compose a list of configurables("*.in" -- makefiles, shell scripts, etc.)
### in subdirs to configure
### Compose their target counterparts
### Configure

configurables=`cd "${srcdir}/src"  &&  find . -name '*.in' -print | grep -v 'config\.h\.in' | grep -v 'configure\.in' | sed "s%\.\(.*\)\.in%$builddir\1:\./src\1\.in%g"`
if test -z "$configurables" ; then
  AC_MSG_ERROR([Cannot find any configurables below \"${srcdir}\"!])
fi


dnl Template substitutions
dnl ----------------------

AC_SUBST(exec_prefix)

AC_SUBST(top_srcdir)
AC_SUBST(srcdir)
AC_SUBST(includedir)
AC_SUBST(builddir)

AC_SUBST(CC)
AC_SUBST(CPP)
AC_SUBST(CXX)
AC_SUBST(AR)
AC_SUBST(LINKDLL)
AC_SUBST(RANLIB)
AC_SUBST(STRIP)

AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(LIBS)

AC_SUBST(internal)
AC_SUBST(serial)

AC_SUBST(script_shell)
AC_SUBST(make_shell)
AC_SUBST(obj_ext)
AC_SUBST(lib_pre)
AC_SUBST(lib_l_pre)
AC_SUBST(lib_ext)
AC_SUBST(lib_l_ext)
AC_SUBST(lib_dll)
AC_SUBST(exe_ext)
AC_SUBST(f_compile)
AC_SUBST(f_outobj)
AC_SUBST(f_outlib)
AC_SUBST(f_libpath)
AC_SUBST(f_outexe)

AC_SUBST(THREAD_LIBS)
AC_SUBST(NETWORK_LIBS)
AC_SUBST(SYBASE_INCLUDE)
AC_SUBST(SYBASE_LIBS)
AC_SUBST(NCBI_C_INCLUDE)
AC_SUBST(NCBI_C_LIBPATH)
AC_SUBST(FASTCGI_INCLUDE)
AC_SUBST(FASTCGI_LIBS)
AC_SUBST(FASTCGI_OBJS)
AC_SUBST(NCBI_SSSDB_INCLUDE)
AC_SUBST(NCBI_SSSDB_LIBS)
AC_SUBST(NCBI_PM_PATH)

AC_SUBST(NCBI_C_ncbi)

AC_SUBST(BINCOPY)
AC_SUBST(CC_FILTER)
AC_SUBST(CXX_FILTER)

AC_SUBST(KeepStateTarget)
AC_SUBST(serial_ws50_rtti_kludge)


#############################################################################
### Create output files and do some post-configuration

# compose a list of target files
changequote(<<, >>)dnl
x_configurables=`echo "$configurables" | sed 's/:[^:][^:]*\.in//g'`
changequote([, ])dnl

# deploy the target directory tree
echo "$x_configurables" | while read x_file ; do
   x_dir=`dirname $x_file`
   test -d "$x_dir"  ||  mkdir -p "$x_dir"
done

# configure the compiler/flag-dependent header
AC_CONFIG_HEADER(${ncbiconf}:config.h.in)

# configure makefiles, shell scripts, etc.
AC_OUTPUT(
${configurables},

builddir="$SAVE_builddir"
exec_prefix="$SAVE_exec_prefix"
x_configurables="$SAVE_x_configurables"
ncbiconf="$SAVE_ncbiconf"
CFLAGS="$SAVE_CFLAGS"
CXXFLAGS="$SAVE_CXXFLAGS"
CPPFLAGS="$SAVE_CPPFLAGS"
LDFLAGS="$SAVE_LDFLAGS"

echo "$x_configurables" | while read x_file ; do
   mv "$x_file" "$x_file.tmp"
   cat "$x_file.tmp" | sed "s%/$builddir%/src%g" > "$x_file"
   rm "$x_file.tmp"
done

find "$exec_prefix/." -name '*.sh' -print | while read x_file ; do
   chmod a+x "$x_file"
done

status_dir="$exec_prefix/status"
test -d  "$status_dir"  ||  mkdir -p "$status_dir"
mv config.status config.cache config.log "$status_dir"/

cat << EOCONF
Configuration completed.

Optional configure flags:
 --without-debug         build non-debug versions of libs and apps
 --without-optimization  turn off optimization flags
 --with-dll              build all libraries as DLLs
 --without-bincopy       do not check whether the copied binaries are identical
 --with-internal         build all projects including the internal ones
 --without-sybase        do not use SYBASE libraries
 --without-ncbi-c        do not use NCBI C Toolkit
 --without-fastcgi       do not use Fast-CGI library
 --without-sssdb         do not use NCBI SSS DB library
 --without-pubmed        do not use NCBI PubMed libraries
 --exec-prefix           specify a non-default build directory name
 --without-debug-sfx     do not add Release/Debug to the build dir name
 --with-hostspec         add full host specs(OS, CPU) to the build dir name

CFLAGS   = $CFLAGS
CXXFLAGS = $CXXFLAGS
CPPFLAGS = $CPPFLAGS
LDFLAGS  = $LDFLAGS
Tools / flags / paths:  $builddir/Makefile.mk
Configuration  header:  $ncbiconf

To build everything:  cd $builddir && make all_r
EOCONF
,

SAVE_builddir="$builddir"
SAVE_exec_prefix="$exec_prefix"
SAVE_x_configurables="$x_configurables"
SAVE_ncbiconf="$ncbiconf"
SAVE_CFLAGS="$CFLAGS"
SAVE_CXXFLAGS="$CXXFLAGS"
SAVE_CPPFLAGS="$CPPFLAGS"
SAVE_LDFLAGS="$LDFLAGS"
)
